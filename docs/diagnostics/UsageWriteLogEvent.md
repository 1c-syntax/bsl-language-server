# Неверное использование метода "ЗаписьЖурналаРегистрации" (UsageWriteLogEvent)

|      Тип      |    Поддерживаются<br>языки    |     Важность     |    Включена<br>по умолчанию    |    Время на<br>исправление (мин)    |               Теги                |
|:-------------:|:-----------------------------:|:----------------:|:------------------------------:|:-----------------------------------:|:---------------------------------:|
| `Дефект кода` |         `BSL`<br>`OS`         | `Информационный` |              `Да`              |                 `1`                 |    `standard`<br>`badpractice`    |

<!-- Блоки выше заполняются автоматически, не трогать -->
## Описание диагностики
<!-- Описание диагностики заполняется вручную. Необходимо понятным языком описать смысл и схему работу -->
Важно правильно с определенной дотошностью указывать основные параметры при записи в Журнал регистрации.

Недопустимо прятать исключения.
При обработке исключений обязательно нужно выполнять запись в журнал регистрации с полным представлением ошибки.
Для этого нужно добавить в комментарий события результат `ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())`

Нельзя пропускать 2й параметр Уровень журнала регистрации. Если его не указать, по умолчанию 1С применит уровень ошибки Информация, и данная запись может потеряться в потоке записей.

Нельзя пропускать и 5й параметр - комментарий к событию записи в журнал регистрации.

## Примеры
<!-- В данном разделе приводятся примеры, на которые диагностика срабатывает, а также можно привести пример, как можно исправить ситуацию -->

Примеры неверного кода
```bsl
    ЗаписьЖурналаРегистрации("Событие");// ошибка
    ЗаписьЖурналаРегистрации("Событие", УровеньЖурналаРегистрации.Ошибка);// ошибка
    ЗаписьЖурналаРегистрации("Событие", УровеньЖурналаРегистрации.Ошибка, , );// ошибка
    ЗаписьЖурналаРегистрации("Событие", , , , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

    ЗаписьЖурналаРегистрации("Событие", УровеньЖурналаРегистрации.Ошибка, , , );// ошибка

    Попытка
      КодСервер();
    Исключение
      ЗаписьЖурналаРегистрации("Событие", УровеньЖурналаРегистрации.Ошибка, , ,
        ОписаниеОшибки()); // ошибка
      ЗаписьЖурналаРегистрации("Событие", УровеньЖурналаРегистрации.Ошибка, , ,
        "Комментарий 1"); // ошибка
    КонецПопытки;
```

Правильный код
```bsl
    Попытка
      КодСервер();
    Исключение

      ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
      ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"), УровеньЖурналаРегистрации.Ошибка, , ,
         ТекстОшибки);
    КонецПопытки;

    Попытка
      КодСервер();
    Исключение

      ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
      ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"), УровеньЖурналаРегистрации.Ошибка, , ,
         ТекстОшибки);

      ВызватьИсключение;
    КонецПопытки;
```

## Двойная запись в журнал регистрации

При вызове методов `ВызватьИсключение;` и `ЗаписьЖурналаРегистрации` необходимо соблюдать определённый порядок, не допускающий двойной записи текста ошибки в журнал регистрации.

Неправильно:
```
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    Попытка 
        МенеджерЗаписи.Записать();
    Исключение
        ЗаписьЖурналаРегистрации(
        ВызватьИсключение "Не удалось записать настройки ""Яндекс"" по причине" + Инфо;
    КонецПопытки;
КонецПроцедуры
```
В общем случае не рекомендуется перехватывать исключения. В частности не нужно перехватывать исключения только ради выдачи сообщения об ошибке. Необработанное исключение в любом случае будет записано в журнал регистрации, если исключение возникло на сервере.

Правильно:
```
&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    МенеджерЗаписи.Записать();
КонецПроцедуры
```

## Варианты использования

Одновременная запись в ЖР и вызов исключения возможны при пробросе исключения на клиент, где нет записи в ЖР).

Корректный пример с ИТС https://its.1c.ru/db/v8std/content/499/hdoc п.3.1:
```
&НаСервере
Процедура ВыполнитьОперацию()
  Попытка
    // код, приводящий к вызову исключения
    ....
  Исключение
    // Запись события в журнал регистрации для системного администратора.
    ЗаписьЖурналаРегистрации(НСтр("ru = 'Выполнение операции'"),
       УровеньЖурналаРегистрации.Ошибка,,,
       ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
    ВызватьИсключение;
  КонецПопытки;
КонецПроцедуры
// и тогда на клиенте:
Попытка
    ВыполнитьОперацию();
Исключение
    ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
    ПоказатьПредупреждение(,НСтр("ru = 'Операция не может быть выполнена по причине:'") + Символы.ПС + ТекстСообщения);
КонецПопытки;
```

Таким образом, либо проброс исключения в верхнеуровневую попытку без записи в ЖР:
```
Исключение
    ОтменитьТранзакцию();
    ВызватьИсключение; // <-- вложенная попытка
КонецПопытки;
```
либо передача текста ошибки:
```
Функция ОтправитьПоПочте()
    Попытка
        // ...
    Исключение
        ТекстОшибки = "Не удалось создать файл табличного документа: " + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        ЗаписьЖурналаРегистрации("Отправка ПФ на почту клиенту", УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
		Возврат ТекстОшибки;        
    КонецПопытки;
КонецФункции
```

## Источники
<!-- Необходимо указывать ссылки на все источники, из которых почерпнута информация для создания диагностики -->
<!-- Примеры источников

* Источник: [Стандарт: Тексты модулей](https://its.1c.ru/db/v8std#content:456:hdoc)
* Полезная информация: [Отказ от использования модальных окон](https://its.1c.ru/db/metod8dev#content:5272:hdoc)
* Источник: [Cognitive complexity, ver. 1.4](https://www.sonarsource.com/docs/CognitiveComplexity.pdf) -->

* [Использование Журнала регистрации](https://its.1c.ru/db/v8std#content:498:hdoc)
* [Перехват исключений в коде](https://its.1c.ru/db/v8std#content:499:hdoc)

## Сниппеты

<!-- Блоки ниже заполняются автоматически, не трогать -->
### Экранирование кода

```bsl
// BSLLS:UsageWriteLogEvent-off
// BSLLS:UsageWriteLogEvent-on
```

### Параметр конфигурационного файла

```json
"UsageWriteLogEvent": false
```
